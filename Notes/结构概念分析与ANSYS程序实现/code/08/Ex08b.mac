!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第08讲：动力学分析入门
!!!!算例Ex08b
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filname,Ex08b

/prep7
L=10.0                !!跨度
NN=200                !!分段数量
Ex=2.06e11    !!弹性模量
m0=1000            !!附加质量kg/m
h=0.4                !!方管截面宽度
b=0.2                !!方管截面高度
t=0.01            !!方管壁厚

!!!!!前处理
/prep7
et,1,beam188            !!定义单元类型
keyopt,1,3,2            !!高阶形函数开关
mp,ex,1,Ex                !!定义材料特性
sectype,1,beam,hrec,,6             !!定义截面类型
secoffset,cent                                !!截面按形心对中
secdata,h,b,t,t,t,t,0,0,0,0        !!定义截面参数
seccontrols,,,,m0                                !!定义附加质量

!!!!!定义节点和单元
dx=L/NN
*do,i,1,NN+1
  n,i,(i-1)*dx,0,0
*enddo
*do,i,1,NN
  e,i,i+1
*enddo

!!!!施加约束
d,all,ux,0
d,all,uz,0
d,all,rotx,0
d,all,roty,0
d,1,uy,0
d,NN+1,uy,0

!!!!然后进行模态分析 
/solu
antype,modal
modopt,lanb,NN-1
mxpand,NN-1
solve

!!!后处理
/post1
set,first
plnsol,u,y

!!!!读取刚度矩阵、质量矩阵、阻尼矩阵
!getKMC,2

!!!!讲课过程中修改命令流
!lumpm,off                        !!!集中质量矩阵开关