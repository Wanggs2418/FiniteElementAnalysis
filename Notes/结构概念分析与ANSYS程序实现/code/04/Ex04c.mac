!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第04讲：认识几何非线性
!!!!算例ex04c
!!!!本算引自同济大学罗永峰博士论文
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filname,Ex04c
*afun, rad  !采用弧度制

/prep7
L=100                !!跨度
a=5.0                !!矢高
Ex=1.0e7             !!弹性模量
Fy=5000           !!中点集中荷载
Area=0.32                 !!截面面积
Iz=1.0                     !!截面惯性矩
h=0.5                        !!截面宽度
NN=50                         !!分段数量


!!定义材料特性、单元类型、实常数
/prep7
et,1,beam3
r,1,Area,Iz,h
mp,ex,1,Ex

!!建立有限元模型
*do,i,1,NN+1
  x=L/NN*(i-1)
  y=a*sin(3.1415926*x/L)
  n,i,x,y,0
*enddo
*do,i,1,NN                        
  e,i,i+1
*enddo
                  
!!施加约束和荷载
d,1,ux,0
d,1,uy,0
d,NN+1,ux,0
d,NN+1,uy,0
nsel,all
f,NN/2+1,fy,-Fy

/solu
time,1
antype,0
nlgeom,on
outres,all,all 
nsubst,500
arclen,on 
solve

/post26
nsol,2,NN/2+1,u,y,  !2号变量，节点y项位移
prod,3,2,,,,,,-1,1,1  !2号变量乘以-1得到3号变量
prod,4,1,,,,,,Fy,1,1  !1号变量time值乘以Fy得到4号变量
xvar,3  !位移作为横轴
plvar,4 !绘制4号变量

!极限承载力为0.60850*5000=3042.5