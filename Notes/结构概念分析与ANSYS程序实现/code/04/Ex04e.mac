!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第04讲：认识几何非线性
!!!!算例Ex04e
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filname,Ex04e

!!!!!!!!!!!参数输入
/prep7
PI=3.1415926535
Ex=1.90e11
d0=0.005
Area=49*d0*d0*PI/4        !!7x7平行钢丝束
L=20
NN=50
dL=L/NN
H=200*1000  !水平力H的大小,初始为200kN
q=1000
q1=q*L/NN

!!!定义单元类型材料类型实常数
/prep7
et,1,link10
keyopt,1,3,0
mp,ex,1,Ex
r,1,Area

!!!!定义节点
*do,i,1,NN+1
  n,i,(i-1)*dL,0,0
*enddo

!!!定义单元
type,1
real,1
mat,1
*do,i,1,NN
  e,i,i+1
*enddo

!!!定义约束
d,1,ux,0
d,1,uy,0
d,1,uz,0
d,NN+1,uy,0
d,NN+1,uz,0

!!!施加荷载
f,NN+1,fx,H
*do,i,2,NN
  f,i,fy,-q1
*enddo

!!!求解
/solu
antype,0
nlgeom,on
nsubst,500
outres,all,all 

/solu
time,1
solve

!!后处理
/post1
plnsol,u,y,0,1.0

!!!!在讲课中修改命令流
/post26 
nsol,2,NN/2+1,u,y,
prod,3,2,,,,,,-1,1,1 
prod,4,1,,,,,,q,1,1 
xvar,3 
plvar,4
nsol,5,NN+1,u,x,
prod,6,1,,,,,,H,1,1 
xvar,5
plvar,6