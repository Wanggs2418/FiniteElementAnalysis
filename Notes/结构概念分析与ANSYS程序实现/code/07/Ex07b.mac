!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!特别注意：子程序及相关数据文件详见附件，解压后放到当前工作目录下!!!!!

!!!!第07讲：钢压杆的极限承载力
!!!!算例Ex07b
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filname,Ex07b

!!!基本参数输入
/prep7
PI=3.1415926535
h=0.15              !!!截面高度
b=0.08                 !!!截面宽度
tw=0.005           !!!腹板厚度
tf=0.008           !!!翼缘厚度
L=2.5                   !!!计算长度
! L=5                   !!!计算长度,绕强轴失稳
v0=L/1000                !!!初始弯曲  
Fy=235.0e6            !!!屈服强度
Ex=2.06e11            !!!弹性模量
Nu=0.3                !!!柏松比
Area=(b*h-(b-tw)*(h-2*tf))    !!!截面面积
Ny=Area*Fy      !!!全截面屈服荷载A*fy
! ins1=-0.6                !!!残余拉应力峰值系数(和Fy的比值）
ins1=0.6                !!!残余拉应力峰值系数(和Fy的比值）
NN=10           !!!分段总数
dL=L/NN

!!!定义单元类型、材料特性
/prep7
et,1,beam188
keyopt,1,3,2    !2次的形函数
mp,ex,1,Ex
mp,prxy,1,Nu
tb,bkin,1,1,2,1 
tbdata,,Fy,0
sectype,1,beam,I,,2
secoffset,cent
secdata,b,b,h,tf,tf,tw

!!!定义单元和节点
*do,i,1,NN+1
 y=dL*(i-1)
 x=sin(y/L*PI)*v0
 n,i,x,y,0
*enddo
n,NN+2,0,0,1e10 !beam188设置方位点，设置z为无穷，绕着弱轴x失稳
! n,NN+2,1e10,0,0 !设置x为无穷，绕强轴z失稳
*do,i,1,NN
 e,i,i+1,NN+2
*enddo

!!!定义边界条件,施加荷载
d,1,ux,0
d,1,uy,0
d,1,uz,0
d,1,roty,0
d,NN+1,ux,0
d,NN+1,uz,0
d,NN+1,roty,0
f,NN+1,fy,-Ny

!!!调用残余应力子程序Makeini
! arg1=288,积分点的总数；arg2=insl残余应力峰值系数
Makeini,288,ins1

!!!求解
/solu
time,1
nlgeom,on
nsubst,200
outres,all,all
! arclen,on
!arctrm,u,0.05*L,NN/2+1,ux
!inistate,write,1,,,,-2,s      !!用于调试，输出初始应力文件
solve

!!!后处理，画荷载位移曲线
/post26
nsol,2,NN/2+1,u,x,ux_2 
xvar,2
plvar,1

/post1
/eshape,1
set,last
plnsol,s,x,0,1