!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第07讲：钢压杆的极限承载力
!!!!算例Ex07a
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filname,Ex07a
*afun, rad

!!!基本参数输入
/prep7
arg1 = 1.2
arg2 = 1000
PI=3.1415926535
h=0.10              !!!截面高度
b=0.05                 !!!截面宽度
tw=0.004           !!!腹板厚度
tf=0.005           !!!翼缘厚度
L=arg1                   !!!计算长度
! L=1.8                   !!!计算长度
v0=L/arg2                !!!初始弯曲  
! v0=L/1000                !!!初始弯曲  
Fy=235.0e6            !!!屈服强度
Ex=2.06e11            !!!弹性模量
Nu=0.3                !!!柏松比
Area=(b*h-(b-tw)*(h-2*tf))    !!!截面面积
Ny=Area*Fy                     !!!全截面屈服荷载A*fy
NN=10                       !!!分段总数
dL=L/NN

!!!定义单元类型、材料特性
/prep7
et,1,beam188
keyopt,1,3,2    !打开高阶形函数
mp,ex,1,Ex
mp,prxy,1,Nu
tb,bkin,1,1,2,1 
tbdata,,Fy,0
sectype,1,beam,I,,2
secoffset,cent
secdata,b,b,h,tf,tf,tw

!!!定义单元和节点
*do,i,1,NN+1
 y=dL*(i-1)
 x=sin(y/L*PI)*v0
 n,i,x,y,0
*enddo
n,NN+2,0,0,1e10
*do,i,1,NN
 e,i,i+1,NN+2
*enddo

!!!定义边界条件,施加荷载
d,1,ux,0
d,1,uy,0
d,1,uz,0
d,1,roty,0
d,NN+1,ux,0
d,NN+1,uz,0
d,NN+1,roty,0
f,NN+1,fy,-Ny

!!!求解
/solu
time,1
nlgeom,on
nsubst,20
outres,all,all
arclen,on
arctrm,u,0.05*L,NN/2+1,ux  !中点
solve
finish


!!!!讲课中修改命令流
!keyopt,1,1,1
!!!后处理，找寻最大稳定系数
/post1
! 总的子步数写入到参数nset
*get,nset,active,0,set,nset
*do,iii,1,nset-1
! 第一个荷载步的第iii个子步
 set, , ,1, , , ,iii,
!  获取当前子步数的time值
 *get,fi0,active,0,set,time
 *if,fi0,ge,fimax,then
   fimax=fi0
 *endif
*enddo
/out,fiH,txt,,append
*vwrite,L,fimax
(2f20.10)
/out

!!!后处理，画荷载位移曲线
/post26
nsol,2,NN/2+1,u,x,ux_2 
xvar,2
plvar,1


! 以a的方式添加内容到fiH.txt文件
! /out,fiH,txt,,append
! 输出构件的长度和承载力极限值的稳定系数
! *vwrite,L,fimax
! 输出个数,20位.10位
! (2f20.10)
! /out

!eresx,no        !!!积分点线性外推选项

!L=arg1                   !!!计算长度
!v0=L/arg2                !!!初始弯曲  