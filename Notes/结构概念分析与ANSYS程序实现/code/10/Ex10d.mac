!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第10讲：国家大剧院钢网壳施工过程分析
!!!!引例Ex10d
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filname,Ex10d

!!!!!参数输入
/prep7
L=10.0            !!跨度
NN=40                !!分段数量
Ex=2.06e11    !!弹性模量
q=5000            !!均布荷载
b=0.2              !!方管截面宽度
h=0.3              !!方管截面高度
t=0.01          !!方管管壁厚度

/prep7
et,1,beam188                                        !!定义单元类型
keyopt,1,3,3                                        !!高阶形函数
sectype,1,beam,hrec,,1             !!定义截面类型
secoffset,cent                                !!截面按形心对中
secdata,h,b,t,t,t,t                        !!定义截面参数
mp,ex,1,Ex                                            !!定义材料特性

et,2,mass21              !!!采用质量单元避免孤点出现
r,2,1

!!!!!定义节点和单元
dx=L/NN
*do,i,1,NN+1
  n,i,(i-1)*dx,0,0
*enddo
*do,i,1,NN
  e,i,i+1
*enddo

!!!定义质量单元
n,NN+2,0,0,0
n,NN+3,L,0,0
type,2
real,2
e,NN+2
e,NN+3
!!!!!约束质量单元所在节点
d,NN+2,all,0
d,NN+3,all,0

!!!进入求解阶段，约束和荷载在/solu中施加和修改
/solu
antype,static
outres,all,all
nsubst,10
nlgeom,on
autots,off            !!关闭自动迭代步长
d,all,uz,0
d,all,rotx,0
d,1,ux,0
d,1,uy,0
d,NN+1,uy,0
d,NN+1,uy,0

!!!!!第1步：梁端简支，施加恒荷载
/solu
time,1
sfbeam,all,2,pres,q
solve

!!!!!第2步：梁端固定，施加活荷载
/solu
time,2
cp,1,rotz,NN+2,1
cp,2,rotz,NN+3,NN+1
sfcum,pres,add
sfbeam,all,2,pres,q
solve

!!!!画弯矩图
/post1
etable,mzi,smisc,3  
etable,mzj,smisc,16  
plls,mzi,mzj,-1,0  