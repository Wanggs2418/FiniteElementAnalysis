!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第01讲：求解简支梁的挠度
!!!!算例ex01a-纯弯
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filename,ex01a

!!!!!参数输入!!!!!
/prep7
L=6                    !!跨度
NN=20              !!分段数量
Ex=2.00e11      !!弹性模量
h=0.4                !!方管截面宽度
b=0.2                !!方管截面高度
t=0.01               !!方管壁厚
ep=0.002          !!边缘纤维应变
Area=b*h-(b-2*t)*(h-2*t)               !!截面面积
Iz=(b*h**3-(b-2*t)*(h-2*t)**3)/12    !!截面惯性矩
Wz=Iz/(h/2)            !!截面模量
Mz=ep*Ex*Wz            !!根据边缘纤维应变计算得到弯矩

!!!!!前处理!!!!!
/prep7
! 定义1号的单元类型为beam3
et,1,beam3                !!定义单元类型
! material property
mp,ex,1,Ex                !!定义材料特性
r,1,Area,Iz,h         !!定义实常数

! 将梁沿着长度方向划分NN=20段
dx=L/NN                      !!每段单元长度
! 梁分成20段，共20+1个节点

! 沿着长度方向定义节点
*do,i,1,NN+1            !!定义节点
  n,i,(i-1)*dx,0,0
*enddo

! 单元有20个
*do,i,1,NN                !!定义单元
  e,i,i+1        
*enddo
d,1,ux,0                !!施加约束
d,1,uy,0
d,NN+1,uy,0
! 矢量沿z轴方向的弯矩
f,1,mz,-Mz            !!施加弯矩，沿着z轴负向
f,NN+1,mz,Mz        !!施加弯矩，沿着z轴正向

!!!!求解!!!!!
/solu
solve

!!!!后处理!!!!!
/post1
/eshape,1                 !!打开形状开关
plnsol,u,y,0,1.0,        !!画变形图

!!讲课过程中修改
!time,1
!nsubst,500 !初始子步500
!nlgeom,on