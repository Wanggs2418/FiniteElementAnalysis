!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第01讲：求解简支梁的挠度
!!!!算例ex01c-beam188-均布荷载
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filename,ex01c

!!!!!参数输入!!!!!
/prep7
L=10                    !!跨度
NN=200                !!分段数量
Ex=2.00e11    !!弹性模量
h=0.4                !!方管截面宽度
b=0.2                !!方管截面高度
t=0.01            !!方管壁厚
q1=1000        !!均布荷载

!!!!!前处理!!!!!
/prep7
! beam-3节点单元，k节点定义方位，翘曲，非线性等
et,1,beam188                !!定义单元类型
keyopt, 1, 3, 2 !对第一种单元的第三个|keyopt(3)设置为2
!空心矩形横截面,2表示划分的精度，值越大越精细
sectype, 1, beam, hrec,, 2 
secoffset, cent   !截面的对中方式
secdata, b, h, t, t, t, t, 0, 0, 0, 0
mp,ex,1,Ex                !!定义材料特性
dx=L/NN                      !!每段单元长度
*do,i,1,NN+1            !!定义节点
  n,i,(i-1)*dx,0,0
*enddo
n, NN+2, 0, 1, 0  !定义beam188的定位点-k
*do,i,1,NN                !!定义单元
  e,i,i+1,NN+2        !beam188需要3个节点
*enddo

! beam188
d,1,ux,0                !!施加约束
d,1,uy,0
d,1,uz,0
d,1,rotx,0
d,NN+1,uy,0
d,NN+1,uz,0
sfbeam,all,1,pres,q1            !!施加均布线荷载

!!!!求解!!!!!
/solu
solve

!!!!后处理!!!!!
/post1
/eshape,1                    !!打开形状开关
plnsol,u,y,0,1.0,    !!画变形图

!!在讲课中修改命令流
!Ashear=2*t*(h-2*t)
!r,1,Area,Iz,h,Area/Ashear,,        !!定义实常数（考虑剪切变形）