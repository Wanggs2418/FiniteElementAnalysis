!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第09讲：国际会议中心单层球面网壳整体稳定性分析
!!!!算例Ex09a
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!引例A：截面折减法
finish
/clear
/filname,Ex09a

!!!参数输入
/prep7
Flag=1            !!!绕強轴=1，绕弱轴=2
*if,Flag,eq,1,then
  L=1.117        !!!该长度和文献完全一样
*else
  L=1.233        !!!该长度和文献完全一样
*endif
L1=0.05            !!!节点域杆件长度
L0=L-L1            !!!杆件长度(不含节点域)
h0=0.22            !!矩形管截面宽度
b0=0.1            !!矩形管截面高度
t0=0.01            !!矩形管壁厚
h1=0.234            !!折减后节点域-矩形管截面宽度
b1=0.055            !!折减后节点域-矩形管截面高度
t1=0.01              !!折减后节点域-矩形管壁厚
Ex=2.06e11                !!!实测弹性模量
Fy=379.1e6                !!!实测屈服强度
Fy1=383.5e6                !!!实测屈服强度-屈服平台段末端
Fu=486.3e6                !!!实测拉断强度

!!!定义单元类型
et,1,beam188            !!!采用Beam188单元 
keyopt,1,3,2            !!!高阶形函数开关

!!!根据实测数据定义材料本构关系
mp,ex,1,Ex                !!!弹性模量
tb,kinh,1,1,4         !!!随动强化模型，和实测数据一样 
tbpt,,Fy/Ex,Fy
tbpt,,0.02,Fy1
tbpt,,0.065,Fu
tbpt,,0.2,Fu

!!!定义截面参数
sectype,1,beam,hrec,,2             !!定义截面类型
secoffset,cent                                !!截面按形心对中
secdata,b0,h0,t0,t0,t0,t0            !!定义截面参数
sectype,2,beam,hrec,,2             !!定义截面类型
secoffset,cent                                !!截面按形心对中
secdata,b1,h1,t1,t1,t1,t1            !!定义截面参数

!!!输入节点和单元
n,1,0,0,0
*do,i,1,4
  n,10+i,i*L1/4,0,0
  n,20+i,0,i*L1/4,0
  n,30+i,-i*L1/4,0,0
  n,40+i,0,-i*L1/4,0
*enddo
*do,i,5,8
  n,10+i,L1+(i-4)*L0/4,0,0
  n,20+i,0,L1+(i-4)*L0/4,0
  n,30+i,-L1-(i-4)*L0/4,0,0
  n,40+i,0,-L1-(i-4)*L0/4,0
*enddo

!!!输入单元
secnum,2
e,1,11
e,1,21
e,1,31
e,1,41
*do,i,1,3
 e,10+i,10+i+1
 e,20+i,20+i+1
 e,30+i,30+i+1
 e,40+i,40+i+1
*enddo
secnum,1
*do,i,4,7
 e,10+i,10+i+1
 e,20+i,20+i+1
 e,30+i,30+i+1
 e,40+i,40+i+1
*enddo

!!!定义约束和荷载
d,28,ux,0
d,28,uy,0
d,28,uz,0
d,28,roty,0
d,48,ux,0
d,48,uy,0
d,48,uz,0
d,48,roty,0
*if,Flag,eq,1,then        !!!绕強轴
 f,18,fz,100000
 f,38,fz,100000
 d,1,uz,0
*else                                    !!!绕弱轴
 f,18,fy,100000
 f,38,fy,100000
 d,1,uy,0
*endif

!!!画单元
/eshape,1
eplot

/solu
time,1
nlgeom,on                        !!!考虑几何非线性        
nsubst,200
outres,all,all
solve

/post26
nsol,2,18,u,z,uz_18 
nsol,3,18,u,y,uy_18
*if,Flag,eq,1,then 
 xvar,2
*else
 xvar,3
*endif
plvar,1