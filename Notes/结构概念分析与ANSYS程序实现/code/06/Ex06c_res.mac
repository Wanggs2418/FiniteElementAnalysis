!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第06讲：认识材料非线性
!!!!算例Ex06c
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filename,Ex06c

/prep7
L=2.00                !!钢板长度,为避免应力不均匀的现象，最后取中间1m长    
b=0.12                !!钢板宽度
t=0.01                !!钢板厚度
Nx=100                !!纵向划分数量
Ny=12                    !!横向划分数量
Ins1=120e6        !!残余拉应力峰值
Ins2=-60e6        !!残余压应力峰值
Fy=200e6            !!屈服强度
Ex=2.00e11        !!弹性模量
Nu=0.0                !!泊松比，尽量减小泊松效应
q0=200e6*t*0.999        !!外荷载，*1时全截面屈服，故*0.999

!!!定义单元类型，实常数和材料特性
/prep7
et,1,shell181
keyopt,1,3,2  !完全积分单元，尤其针对板受弯
r,1,t
mp,Ex,1,Ex
mp,prxy,1,Nu
tb,bkin,1,1,2,1
tbdata,,Fy,0            !!理想弹塑性模型

!!!直接建立有限元模型，建立节点，便于控制节点编号
*do,i,1,Nx+1
*do,j,1,Ny+1
  n0=(i-1)*Nx+j
  x=(i-1)*L/Nx-L/2
  y=(j-1)*b/Ny-b/2
  z=0
  n,n0,x,y,z
*enddo
*enddo

!!!直接建立有限元模型，建立单元
type,1
*do,i,1,Nx
*do,j,1,Ny
  ni=(i-1)*Nx+j
  nj=ni+Nx
  nk=nj+1
  nl=nk-Nx
  e,ni,nj,nk,nl
*enddo
*enddo

!!!施加约束
nsel,s,loc,x,0,0  !约束刚体位移
d,all,ux,0
nsel,r,loc,y,0,0
d,all,uy,0
nsel,s,loc,x,-L/2,-L/2
nsel,a,loc,x,L/2,L/2
d,all,uz,0
nsel,all

!!求解设置
/solu
nsubst,20
autots,off  !关闭自动迭代步长
outres,all,all
nlgeom,on

!!输入残余应力
/solu
! set 设置残余应力的上输入方式，-2表示单元坐标系
inistate,set,csys,-2                !!按单元坐标系输入残余应力
! 设置输入方式，应力输入
inistate,set,dtyp,stre            !!输入应力值
*do,i,1,Nx
*do,j,Ny/3+1,2*Ny/3
 n0=(i-1)*Ny+j
 inistate,define,n0,,,,Ins1,0,0,0,0,0
*enddo
*enddo
*do,i,1,Nx
*do,j,1,Ny/3
 n0=(i-1)*Ny+j
 inistate,define,n0,,,,Ins2,0,0,0,0,0
*enddo
*do,j,2*Ny/3+1,Ny
 n0=(i-1)*Ny+j
 inistate,define,n0,,,,Ins2,0,0,0,0,0
*enddo
*enddo

!!!施加拉力
time,1
esel,s,,,(Nx-1)*Ny+1,Nx*Ny,1
! 根据i,j,k的方向，分别选择4，6号边
sfe,all,4,pres,1,-q0
esel,s,,,1,Ny,1
sfe,all,6,pres,1,-q0
esel,all
solve

time,2
esel,s,,,(Nx-1)*Ny+1,Nx*Ny,1
sfe,all,4,pres,1,0
esel,s,,,1,Ny,1
sfe,all,6,pres,1,0
esel,all
solve

/post1
set,last
plnsol,s,x,0,1

!!讲课中补充,仅选择中部钢板画应力图
!nsel,s,loc,x,-L/4,L/4
!esln