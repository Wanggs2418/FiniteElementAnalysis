!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!第06讲：认识材料非线性
!!!!算例Ex06b-延性的验证，塑性弯矩
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
/clear
/filename,Ex06b

!!!!!参数输入!!!!!
/prep7
L=6                    !!跨度
NN=20                !!分段数量
Ex=2.06e11    !!弹性模量
Fy=235e6        !!屈服强度
Nu=0.3            !!泊松比
h=0.4                !!方管截面宽度
b=0.2                !!方管截面高度
t=0.01            !!方管壁厚
epy=Fy/Ex        !!屈服应变
epp=0.025        !!屈服平台段末端对应的应变
Area=b*h-(b-2*t)*(h-2*t)                    !!截面面积
Iz=(b*h**3-(b-2*t)*(h-2*t)**3)/12    !!截面惯性矩
Wz=Iz/(h/2)                !!截面模量
Mzy=Fy*Wz                    !!根据边缘纤维应变计算得到弯矩
h1=h*epy/epp            !!弹性核高度
M1=Fy*t*h1*h1/3                    !!弹性核对应的弯矩
M2=Fy*t*(h-h1)*(h+h1)/2        !!塑性区腹板的弯矩
M3=Fy*t*(b-2*t)*(h-t)            !!塑性区翼缘的弯矩
Mzp=M1+M2+M3                        !!边缘纤维为ep1时的弯矩

!!!!!单元类型、截面和材料
/prep7
et,1,beam188                !!定义单元类型
keyopt,1,3,2                !!高阶形函数开关
sectype,1,beam,hrec,,6                !!定义截面类型
secoffset,cent                                !!截面按形心对中
secdata,b,h,t,t,t,t                        !!定义截面参数
mp,ex,1,Ex                                        !!定义材料特性
mp,prxy,1,Nu
tb,bkin,1,1,2,1
tbdata,,Fy,0

!!!!定义单元和节点
dx=L/NN                            !!每段单元长度
*do,i,1,NN+1                !!定义节点
  n,i,(i-1)*dx,0,0
*enddo
n,NN+2,0,1,0
*do,i,1,NN                !!定义单元
  e,i,i+1,NN+2        
*enddo

!!!!施加荷载和约束
d,1,ux,0                    !!施加约束
d,1,uy,0
d,NN+1,uy,0
d,all,rotx,0
d,all,uz,0

!!!!求解!!!!!
/solu
nsubst,200
autots,on
outres,all,all
nlgeom,on

time,1
f,1,mz,-Mzy            !!施加弯矩
f,NN+1,mz,Mzy        !!施加弯矩
solve

time,2
f,1,mz,-Mzp            !!施加弯矩
f,NN+1,mz,Mzp        !!施加弯矩
solve

!!!!后处理!!!!!
/post26
nsol,2,NN/2+1,u,y
/post1
/eshape,1                    !!打开形状开关
plnsol,u,y,0,1.0     !!画变形图

!Mzp=Wz*epp*Ex